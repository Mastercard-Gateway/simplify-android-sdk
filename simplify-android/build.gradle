apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka-android'


android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionName libraryVersionName

        consumerProguardFiles 'proguard.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        debug
        release
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        release.java.srcDirs += 'src/release/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

// define an 'optional'
configurations {
    optional
    implementation.extendsFrom optional
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha04'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha5'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.google.code.gson:gson:2.8.2'

    // optional for rxjava
    optional 'io.reactivex.rxjava2:rxjava:2.2.7'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.2'
    testImplementation 'org.mockito:mockito-core:2.25.0'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

task dokkaJavadocs(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "${project.buildDir}/javadoc"
}

task dokkaJavadocsJar(type: Jar, dependsOn: dokkaJavadocs) {
    classifier = 'javadoc'
    from dokkaJavadocs.outputDirectory
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId = 'com.simplify'
            artifactId project.name
            version = libraryVersionName

            afterEvaluate {
                artifact bundleReleaseAar
                artifact dokkaJavadocsJar
                artifact androidSourcesJar
            }

            pom.withXml {
                def root = asNode()

                // adds basic info
                root.appendNode('name', 'Simplify Android SDK')
                root.appendNode('description', 'The Android SDK for Simplify Commerce')
                root.appendNode('url', 'https://www.simplify.com/commerce/docs/sdk/android')

                // adds license info
                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The MIT License')
                license.appendNode('url', 'http://opensource.org/licenses/MIT')
                license.appendNode('distribution', 'repo')

                // adds source control info
                def scm = root.appendNode('scm')
                scm.appendNode('url', 'https://github.com/simplifycom/simplify-android-sdk')
                scm.appendNode('connection', 'https://github.com/simplifycom/simplify-android-sdk.git')

                // adds dependencies
                def dependenciesNode = root.appendNode('dependencies')
                configurations.implementation.allDependencies.each { dp ->
                    if (dp.group) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dp.group)
                        dependencyNode.appendNode('artifactId', dp.name)
                        dependencyNode.appendNode('version', dp.version)

                        if (configurations.optional.allDependencies.contains(dp)) {
                            dependencyNode.appendNode('optional', true)
                        }
                    }
                }

                // adds developer info
                def developers = root.appendNode('developers')
                def dev = developers.appendNode('developer')
                dev.appendNode('id', 'simplifycom')
                dev.appendNode('name', 'Simplify Commerce')
                dev.appendNode('email', 'support@simplify.com')
            }
        }
    }
}


bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['aar']
    publish = true
    pkg {
        repo = 'Android'
        name = 'simplify-android'
        userOrg = 'simplify'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/simplifycom/simplify-android-sdk.git'
        version {
            name = libraryVersionName
        }
    }
}
